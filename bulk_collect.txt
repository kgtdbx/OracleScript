DECLARE
   TYPE numtab IS TABLE OF NUMBER INDEX BY PLS_INTEGER;
   CURSOR c1 IS
   SELECT employee_id
   FROM employees
   WHERE department_id = 80;
   empids numtab;
   rows PLS_INTEGER := 10;
BEGIN
   OPEN c1;
   LOOP -- Fetch 10 rows or fewer in each iteration
      FETCH c1 BULK COLLECT INTO empids LIMIT rows;
      EXIT WHEN empids.COUNT = 0; -- Not: EXIT WHEN c1%NOTFOUND
      DBMS_OUTPUT.PUT_LINE ('------- Results from Each Bulk Fetch --------');
      FOR i IN 1..empids.COUNT LOOP
         DBMS_OUTPUT.PUT_LINE ('Employee Id: ' || empids(i));
      END LOOP;
   END LOOP;
   CLOSE c1;
END;
/